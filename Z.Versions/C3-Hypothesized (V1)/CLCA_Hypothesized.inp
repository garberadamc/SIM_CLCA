TITLE:
Hypothesized Model
DATA:
FILE = "/Users/agarber/github/SIM_CLCA/C3-Hypothesized/CLCA_Hypothesized.dat";
 
VARIABLE:
NAMES = primary change interupt initiat engage approach response expect new same
     relative objects sequence trans avoid control touch; 
 MISSING=.;
 categorical = primary-touch; 
    usevar = primary-touch;
    classes = c(5); 
ANALYSIS:
estimator = mlr; 
    type = mixture;
    starts = 0;
    STSEED = 279529; !!! USE SEED TO REPLICATE THESIS RESULTS !!!
   
MODEL:
!!! NAME & LABEL PARAMETERS !!!
  
     %OVERALL%
  
     [ c#1 ];
     [ c#2 ];
     [ c#3 ];
     [ c#4 ];
     
     %C#1%       !!!High_Flex!!!
  
     [ primary$1 ](t1);
     [ change$1  ](t2);
     [ interupt$1](t3);
     [ initiat$1 ](t4);
     [ engage$1  ](t5);
     [ approach$1](t6);
     [ response$1](t7);
     [ expect$1  ](t8);
     [ new$1     ](t9);
     [ same$1    ](t10);
     [ relative$1](t11);
     [ objects$1 ](t12);
     [ sequence$1](t13);
     [ trans$1   ](t14);
     [ avoid$1   ](t15);
     [ control$1 ](t16);
     [ touch$1   ](t17);
  
     %C#2%       !!!ScIn_Flex!!!
  
     [ primary$1 ](t18);
     [ change$1  ](t19);
     [ interupt$1](t20);
     [ initiat$1 ](t21);
     [ engage$1  ](t22);
     [ approach$1](t23);
     [ response$1](t24);
     [ expect$1  ](t25);
     [ new$1     ](t26);
     [ same$1    ](t27);
     [ relative$1](t28);
     [ objects$1 ](t29);
     [ sequence$1](t30);
     [ trans$1   ](t31);
     [ avoid$1   ](t32);
     [ control$1 ](t33);
     [ touch$1   ](t34);
  
     %C#3%        !!!LocSnsLo!!!
  
     [ primary$1 ](t52);
     [ change$1  ](t53);
     [ interupt$1](t54);
     [ initiat$1 ](t55);
     [ engage$1  ](t56);
     [ approach$1](t57);
     [ response$1](t58);
     [ expect$1  ](t59);
     [ new$1     ](t60);
     [ same$1    ](t61);
     [ relative$1](t62);
     [ objects$1 ](t63);
     [ sequence$1](t64);
     [ trans$1   ](t65);
     [ avoid$1   ](t66);
     [ control$1 ](t67);
     [ touch$1   ](t68);
  
     %C#4%     !!!Env_Flex !!!  
  
     [ primary$1 ](t35);
     [ change$1  ](t36);
     [ interupt$1](t37);
     [ initiat$1 ](t38);
     [ engage$1  ](t39);
     [ approach$1](t40);
     [ response$1](t41);
     [ expect$1  ](t42);
     [ new$1     ](t43);
     [ same$1    ](t44);
     [ relative$1](t45);
     [ objects$1 ](t46);
     [ sequence$1](t47);
     [ trans$1   ](t48);
     [ avoid$1   ](t49);
     [ control$1 ](t50);
     [ touch$1   ](t51);
  
     %C#5%         !!!Low_Flex!!!
  
     [ primary$1 ](t69);
     [ change$1  ](t70);
     [ interupt$1](t71);
     [ initiat$1 ](t72);
     [ engage$1  ](t73);
     [ approach$1](t74);
     [ response$1](t75);
     [ expect$1  ](t76);
     [ new$1     ](t77);
     [ same$1    ](t78);
     [ relative$1](t79);
     [ objects$1 ](t80);
     [ sequence$1](t81);
     [ trans$1   ](t82);
     [ avoid$1   ](t83);
     [ control$1 ](t84);
     [ touch$1   ](t85);
    
MODEL CONSTRAINT:

  !!!       THRESHOLD BOUNDARIES            !!!
  !!! -.85 (THRESHOLD) ~ .70 (PROBABILTY)   !!!
  !!!  .85 (THRESHOLD) ~ .30 (PROBABILTY)   !!!
     
  ! LABELS C1-C5 BELOW REFLECT ORDER OF CLASSES IN PLOT (NOT MPLUS C# LABELS ABOVE)
  
     !   C1    |   C2    |   C3    |   C4   |    C5  !
     !High_Flex|ScIn_Flex|Env_Flex |LocSnsLo|Low_Flex!
     !_________|_________|_________|________|________!
      t1 <-.85; t18<-.85; t35> .85;           t69> .85;  !!!U1 !!!
      t2 <-.85; t19<-.85; t36> .85;           t70> .85;  !!!U2 !!!
      t3 <-.85; t20<-.85; t37> .85;           t71> .85;  !!!U3 !!!
      t4 <-.85; t21<-.85; t38> .85;           t72> .85;  !!!U4 !!!
      t5 <-.85; t22<-.85; t39> .85;           t73> .85;  !!!U5 !!!
      t6 <-.85; t23<-.85; t40> .85;           t74> .85;  !!!U6 !!!
      t7 <-.85; t24<-.85; t41> .85;           t75> .85;  !!!U7 !!!
      t8 <-.85; t25<-.85; t42> .85;           t76> .85;  !!!U8 !!!
      
      t9 <-.85; t26> .85; t43<-.85; t60> .85; t77> .85;  !!!U9 !!!
      t10<-.85; t27> .85; t44<-.85; t61> .85; t78> .85;  !!!U10!!!
      t11<-.85; t28> .85; t45<-.85; t62> .85; t79> .85;  !!!U11!!!
      t12<-.85; t29> .85; t46<-.85; t63<-.85; t80> .85;  !!!U12!!!
      t13<-.85; t30> .85; t47<-.85; t64<-.85; t81> .85;  !!!U13!!!
      t14<-.85; t31> .85; t48<-.85; t65<-.85; t82> .85;  !!!U14!!!
      t15<-.85; t32> .85; t49<-.85; t66> .85; t83> .85;  !!!U15!!!
      t16<-.85; t33> .85; t50<-.85; t67> .85; t84> .85;  !!!U16!!!
      t17<-.85; t34> .85; t51<-.85; t68> .85; t85> .85;  !!!U17!!!
      
  !!!       EQUALITY (& INEQUALITY) CONSTRAINTS       !!!
     
    !  C1vC2  |  C3vC4  |  C4vC5  !
    !_________|_________|_________!
     t1 = t18; t35=-t52;           !!!U1 !!!
     t2 = t19; t36=-t53;           !!!U2 !!!
     t3 = t20; t37=-t54;           !!!U3 !!!
     t4 = t21; t38=-t55;           !!!U4 !!!
     t5 = t22; t39=-t56;           !!!U5 !!!
     t6 = t23; t40=-t57;           !!!U6 !!!
     t7 = t24; t41=-t58;           !!!U7 !!!
     t8 = t25; t42=-t59;           !!!U8 !!!
     t9 =-t26;                     !!!U9 !!!
     t10=-t27;                     !!!U10!!!
     t11=-t28;                     !!!U11!!!
     t12=-t29;           t63=-t80; !!!U12!!!
     t13=-t30;           t64=-t81; !!!U13!!!
     t14=-t31;           t65=-t82; !!!U14!!!
     t15=-t32;                     !!!U15!!!
     t16=-t33;                     !!!U16!!!
     t17=-t34;                     !!!U17!!!

  
OUTPUT:
tech14;
PLOT:
type = plot3; 
          series = primary-touch(*);
